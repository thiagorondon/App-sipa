#!/usr/bin/env perl
#
# Aware TI, 2010, http://www.aware.com.br
# Thiago Rondon <thiago@aware.com.br>
#


=head1 NAME

Sipa - Yet Another Pcap Sniffer for SIP

=head2 INSTALL

To make the instructions a little easier, this assume that you have
Net::Pcap:Easy and MooseX::Getopt modules installed, but if you dont have,
dont worry, install with cpan application.

    cpan Net::Pcap::Easy
    cpan MooseX::Getopt

Than, just do that:

    perl Makefile.PL
    make
    make install

Your sipa is in your /usr/bin now.

=head2 OPTIONS

=head3 --port [Int]

Default value: 5060

=head3 --dev [Str]

Default value: eth0

=head3 --promiscous

Default value: True

=head3 --verbose

Show all the packet.

=head3 --out [filepath]

Put output in [filepath].

=head3 --codecs

Show the codecs in output.

=head3 --txt [string]

String for match in the packets.


=head1 AUTHOR

Thiago Rondon <thiago.rondon@gmail.com>

=head1 LICENSE

This library is free software. You can redistribute it and/or modify
it under the same terms as Perl itself.

=cut


package Sipa;

use Moose;
use Net::Pcap::Easy;
use Data::Dumper;

has 'opt' => (is => 'rw', isa => 'Object');
has 'fh' => (is => 'rw', isa => 'FileHandle');
has 'spo' => (is => 'rw', isa => 'Str');

sub _prepare {
    my $self = shift;
   
    if ($self->opt->out) {
        open my $fh, '>>', $self->opt->out or die $!;
        $self->fh($fh);
    }
}

sub _print {
    my $self = shift;
    my @out = @_;
    if ($self->opt->out) {
        my $fh = $self->fh;
        print $fh (@out);
    } else {
        print (@out);
    }
}

sub _show_codecs {
    my $self = shift;
    my @data = split(/\n/, $self->spo);
    for my $line (@data) {
        print (' ' x 16 . '->' . $line . "\n") if $line =~ 'a=rtpmap';
    }
}

sub _check_txts {
    my $self = shift;
    if ($self->opt->txt) {
        return 0 if $self->opt->txt !~ $self->spo;
    }   
    return 1;
}

sub get {
    my $self = shift;
    $self->_prepare;

    my $npe = Net::Pcap::Easy->new(
        dev     =>  $self->opt->dev,
        filter  =>  $self->opt->filter,
        packet_per_loop => 10,
        bytes_to_capture => 1024,
        timeout_in_ms => 0,
        promiscous => $self->opt->promiscous,

        udp_callback => sub {
            my ($npe, $ether, $ip, $spo) = @_;
                $self->spo($spo->{data});
                return if !$self->_check_txts;
 
                $self->_print ("SIP:$ip->{src_ip}:$spo->{src_port} -> ");
                $self->_print ("$ip->{dest_ip}:$spo->{dest_port}");
                
                
                my @data = split(/\n/, $spo->{data});
                $self->_print (' : ' . $data[0] . "\n");

                if ($self->opt->codecs) {
                    $self->_show_codecs;
                }

                if ($self->opt->verbose) {
                    $self->_print ("npe " . "-" x 30 . "\n");
                    $self->_print (Dumper($npe));
                    $self->_print ("ether " . "-" x 30 . "\n");
                    $self->_print (Dumper($ether));
                    $self->_print ("ip " . "-" x 30 . "\n");
                    $self->_print (Dumper($ip));
                    $self->_print ("spo " . "-" x 30 . "\n");
                    $self->_print (Dumper($spo));
                }
        },
    );

    1 while $npe->loop;
}

1;

####

package main;

use Moose;
use FindBin qw($Bin);
use lib "$Bin/lib";
use Sipa::Getopt;

sub _main {
    my $app = Sipa::Getopt->new_with_options();
    my $sipa = Sipa->new;
    $sipa->opt($app);
    $sipa->get();

}

_main;


